#use "updater1c"

// ****************************************************************************
// Переменные модуля
// ****************************************************************************

Перем errors;		// Признак того, что при выполнении скрипта были ошибки.
Перем updater;		// Обновлятор, через который мы получаем информацию о базе,
					// а также вызываем различные функции обновлятора.
Перем connector;	// Коннектор для подключения к базе.
Перем v8;			// Само подключение к базе через коннектор.

// ****************************************************************************
// Ваш код для выполнения обновлятором
// ****************************************************************************

Процедура Главная()

	//=========== НАЧАЛО КОДА ДОБАВЛЕНИЯ ПОЛЬЗОВАТЕЛЕЙ ==========

	
	Т = v8.NewObject("ТаблицаЗначений"); 
	Т.Колонки.Добавить("ПолноеИмя");
	Т.Колонки.Добавить("Имя");
	Т.Колонки.Добавить("Пароль");
	Т.Колонки.Добавить("ВходВПрограммуРазрешен");
	Т.Колонки.Добавить("АутентификацияСтандартная");
	Т.Колонки.Добавить("АутентификацияОС");
	Т.Колонки.Добавить("ПользовательОС");
	Т.Колонки.Добавить("ЗапрещеноИзменятьПароль");
	Т.Колонки.Добавить("ПоказыватьВСпискеВыбора");
	Т.Колонки.Добавить("Профили");
	Т.Колонки.Добавить("Удалить");
	Т.Колонки.Добавить("Переименовать");
	
	
	//********** НАЧАЛО: ЗДЕСЬ ОПИСЫВАЕМ ПОЛЬЗОВАТЕЛЕЙ ***************
	
	С = Т.Добавить(); 
	С.ПолноеИмя = "Пользователь 2";
	С.Имя = "Пользователь2";
	С.Пароль = "";
	С.ВходВПрограммуРазрешен = истина;
	С.АутентификацияСтандартная = ложь;
//	С.АутентификацияОС = ложь;
//	С.ПользовательОС = Неопределено;
	С.АутентификацияОС = истина;
	С.ПользовательОС = "\\SVD\4110";
	С.ЗапрещеноИзменятьПароль = ложь;
	С.ПоказыватьВСпискеВыбора = ложь;
	С.Профили = "Бухгалтер, Главный бухгалтер, Добавление и изменение дат запрета изменения, Старший кадровик-расчетчик, Синхронизация данных с другими программами";
	//С.Профили = "Бухгалтер";
	С.Переименовать = истина;

		
	//********** КОНЕЦ ОПИСАНИЯ ПОЛЬЗОВАТЕЛЕЙ ***************
	
	Для Каждого С ИЗ Т Цикл
		Если С.Переименовать = истина Тогда
			//Если надо удалять пользователей
			Для Каждого ПользовательИБ ИЗ v8.ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
			
				//Сообщить("" +  ПользовательИБ.ПользовательОС + " " + XMLСтрока(ПользовательИБ.АутентификацияОС));
				
				//Ищем по пользователю ОС с включенной аутентификацией ОС
				Если ПользовательИБ.АутентификацияОС = ложь ИЛИ СокрЛП(ПользовательИБ.ПользовательОС) <> С.ПользовательОС Тогда
					Продолжить;
				КонецЕсли;

				
				//Убираем признак аутентификации ОС
				Если ПользовательИБ.Имя <> С.Имя ИЛИ ПользовательИБ.ПолноеИмя <> С.ПолноеИмя  Тогда
					ПользовательИБ.Имя  = С.Имя;
					ПользовательИБ.ПолноеИмя  = С.ПолноеИмя;
					ПользовательИБ.Записать();
					Сообщить("Установлено имя: " + С.Имя + " и полное имя: " + С.ПолноеИмя + " у пользователя ОС: " + ПользовательИБ.ПользовательОС);
				КонецЕсли;

				//Ищем пользователя базы 1С
				ТекПользователь = v8.Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ПользовательИБ.УникальныйИдентификатор);
				Если ЗначениеЗаполнено(ТекПользователь) И ТекПользователь.Наименование <> С.ПолноеИмя Тогда
					ТекПользовательОбъект = ТекПользователь.ПолучитьОбъект();
					ТекПользовательОбъект.Наименование = С.ПолноеИмя;
					ТекПользовательОбъект.Записать();
					Сообщить("Установлено наименование: " + С.ПолноеИмя + " у пользователя 1с: " + ТекПользователь);
				КонецЕсли;
				
			КонецЦикла;
			Продолжить;
		КонецЕсли;

		Если С.Удалить = истина Тогда
			//Если надо удалять пользователей
			Для Каждого ПользовательИБ ИЗ v8.ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
				//Ищем по пользователю ОС
				Если  СокрЛП(ПользовательИБ.ПользовательОС) <> С.ПользовательОС Тогда
					Продолжить;
				КонецЕсли;

				//Убираем признак аутентификации ОС
				Если ПользовательИБ.АутентификацияОС Тогда
					ПользовательИБ.АутентификацияОС = ложь;
					Сообщить("Сброшен признак аутентификации ОС у пользователя ОС: " + ПользовательИБ.ПользовательОС);
					ПользовательИБ.Записать();
				КонецЕсли;

				//Ищем пользователя базы 1С
				ТекПользователь = v8.Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ПользовательИБ.УникальныйИдентификатор);
				Если ЗначениеЗаполнено(ТекПользователь) И НЕ ТекПользователь.Недействителен Тогда
					ТекПользовательОбъект = ТекПользователь.ПолучитьОбъект();
					ТекПользовательОбъект.Недействителен = истина;
					Сообщить("Установлен признак недействительного пользователя у пользователя 1с: " + ТекПользователь);
					ТекПользовательОбъект.Записать();
				КонецЕсли;
				
			КонецЦикла;
			Продолжить;
		КонецЕсли;
		
		//Создаем пользователя
		ОбновляемыеСвойства = v8.NewObject("Структура");
		Для Инд = 1 По Т.Колонки.Количество() Цикл
			Колонка = Т.Колонки.Получить(Инд - 1);
			Зн = С.Получить(Инд - 1);
			Если Колонка.Имя = "Профили" Тогда
				Продолжить;
			КонецЕсли;
			Если Зн <> Неопределено Тогда
				ОбновляемыеСвойства.Вставить(Колонка.Имя, Зн);
			КонецЕсли;				
		КонецЦикла;
		ОбновляемыеСвойства.Вставить("Действие", "Записать"); //Не уверен, что без этого будет работать
		ОбновляемыеСвойства.Вставить("ПарольУстановлен", истина); 
		ОбновляемыеСвойства.Вставить("АутентификацияOpenID", ложь); 
		
		
		ПользовательИБСуществует = v8.ПользователиИнформационнойБазы.НайтиПоИмени(С.Имя) <> Неопределено;
		
		v8.Пользователи.УстановитьСвойстваПользователяИБ(С.Имя, ОбновляемыеСвойства, НЕ ПользовательИБСуществует); //Создавать нового ставим всегда в Истина
		
		Сообщить(?(ПользовательИБСуществует, "Добавлен", "Обновлен") + " пользователь ИБ: "  + С.Имя);
		
		ТекПользователь = v8.Справочники.Пользователи.НайтиПоНаименованию(ОбновляемыеСвойства.ПолноеИмя, истина);
		ПользовательСуществует = v8.ЗначениеЗаполнено(ТекПользователь);
		
		//Записываем пользователя в 1С, если его еще нет в базе
		Если НЕ ПользовательСуществует Тогда
			Пользователь = v8.Справочники.Пользователи.СоздатьЭлемент();	
			Пользователь.Наименование = ОбновляемыеСвойства.ПолноеИмя;
			Пользователь.ДополнительныеСвойства.Вставить(
			"ОписаниеПользователяИБ", ОбновляемыеСвойства);
			Пользователь.Записать();	
			ТекПользователь = Пользователь.Ссылка;
			Сообщить("Создан пользователь базы: "  + С.Имя);
		КонецЕсли;
	
		МассивПрофили = СтрРазделить(С.Профили, ",");
		Для Каждого ИмяПрофиля ИЗ МассивПрофили Цикл
			ИмяПрофиля = СокрЛП(ИмяПрофиля);
			Если v8.УправлениеДоступомСлужебный.УпрощенныйИнтерфейсНастройкиПравДоступа() Тогда
				ТекПрофиль = v8.Справочники.ПрофилиГруппДоступа.НайтиПоНаименованию(ИмяПрофиля);
				Если Не v8.ЗначениеЗаполнено(ТекПрофиль) Тогда
					Сообщить("Не найден профиль: " + ИмяПрофиля, СтатусСообщения.Важное);
					Продолжить;
				КонецЕсли;
				v8.УправлениеДоступом.ВключитьПрофильПользователю(ТекПользователь, ТекПрофиль);
			Иначе
				ТекГруппа = v8.Справочники.ГруппыДоступа.НайтиПоНаименованию(ИмяПрофиля);
				//Сообщить("" +ТипЗнч(ТекГруппа) + " " + ТекГруппа);
				//Прерват
				Если Не v8.ЗначениеЗаполнено(ТекГруппа) Тогда
					Сообщить("Не найдена группа: " + ИмяПрофиля, СтатусСообщения.Важное);
					Продолжить;
				КонецЕсли;
				
				//Код скопирован из типовой формы ПраваДоступа, несколько упрощен
				ГруппаДоступаОбъект = ТекГруппа.ПолучитьОбъект();
				Добавить = истина;
				Если Добавить Тогда
					Если ГруппаДоступаОбъект.Пользователи.Найти(ТекПользователь, "Пользователь") = Неопределено Тогда
						ГруппаДоступаОбъект.Пользователи.Добавить().Пользователь = ТекПользователь;
					КонецЕсли;
				Иначе
					СтрокаТЧ = ГруппаДоступаОбъект.Пользователи.Найти(ТекПользователь, "Пользователь");
					Если СтрокаТЧ <> Неопределено Тогда
						ГруппаДоступаОбъект.Пользователи.Удалить(СтрокаТЧ);
					КонецЕсли;
				КонецЕсли;
				ГруппаДоступаОбъект.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	//=========== КОНЕЦ КОДА ДОБАВЛЕНИЯ ПОЛЬЗОВАТЕЛЕЙ ==========
	

КонецПроцедуры


// ****************************************************************************
// Служебные процедуры
// ****************************************************************************

Процедура ПриНачалеРаботы()

	errors = Ложь;

	updater = Новый Updater1C;

	// Если в скрипте не планируется использовать
	// подключение к базе - просто закомментируйте
	// две нижние строки.
	connector = updater.CreateConnector();
	v8 = updater.BaseConnectNew(connector);
	
КонецПроцедуры

Процедура ПриОкончанииРаботы()

	Если v8 <> Неопределено Тогда
		Попытка
			ОсвободитьОбъект(v8);
			v8 = Неопределено;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если connector <> Неопределено Тогда
		Попытка
			ОсвободитьОбъект(connector);
			connector = Неопределено;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если updater <> Неопределено Тогда
		Попытка
			ОсвободитьОбъект(updater);
			updater = Неопределено;
		Исключение
		КонецПопытки;
	КонецЕсли;

	Если errors Тогда
		ЗавершитьРаботу(1);
	КонецЕсли;

КонецПроцедуры

// ****************************************************************************
// Инициализация и запуск скрипта
// ****************************************************************************

ПриНачалеРаботы();

Попытка	
	Главная();
	updater.КодПользователяВыполнился();
Исключение
	errors = Истина;
	Сообщить("<span class='red'><b>" + ОписаниеОшибки() + "</b></span>");
КонецПопытки;

ПриОкончанииРаботы();